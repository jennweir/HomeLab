# Google ------------------------------------------------------------------------------------------------------------------

locals {
    wif_pool = "okd-pool"
    wif_provider = "okd-provider"
    avp_gsm_secrets = [
        "okd_cluster_id",
        "project_id",
        "grafana_admin_user",
        "grafana_admin_password",
    ]
}

data "google_project" "okd_homelab" {
    project_id = "okd-homelab"
}

resource "google_project_service" "wif" {
    project            = data.google_project.okd_homelab.project_id
    service            = "sts.googleapis.com"
    disable_on_destroy = false
}

resource "google_storage_bucket" "homelab_bucket" {
    project = data.google_project.okd_homelab.project_id
    name = "jennweir-homelab"
    location = "US"
    force_destroy = true
    public_access_prevention = "inherited"
    uniform_bucket_level_access = true
}

resource "google_storage_bucket_iam_member" "homelab_bucket" {
    bucket = google_storage_bucket.homelab_bucket.name
    role   = "roles/storage.objectViewer"
    member = "allUsers"
}

resource "google_iam_workload_identity_pool" "okd_pool" {
    project                   = data.google_project.okd_homelab.project_id
    workload_identity_pool_id = local.wif_pool
    display_name              = local.wif_pool
    description               = "created with terraform"
}

resource "google_iam_workload_identity_pool_provider" "okd_provider" {
    project = data.google_project.okd_homelab.project_id
    display_name                       = local.wif_provider
    description                        = "created with terraform"
    workload_identity_pool_id          = google_iam_workload_identity_pool.okd_pool.workload_identity_pool_id
    workload_identity_pool_provider_id = local.wif_provider
    attribute_mapping = {
        "google.subject" = "assertion.sub"
    }
    oidc {
        issuer_uri        = "https://storage.googleapis.com/jennweir-homelab"
        allowed_audiences = [
            "//iam.googleapis.com/projects/${data.google_project.okd_homelab.number}/locations/global/workloadIdentityPools/${google_iam_workload_identity_pool.okd_pool.workload_identity_pool_id}/providers/${local.wif_provider}", 
            "openshift"
        ]
    }
}

resource "google_service_account" "test_gsm_accessor" {
    account_id   = "test-gsm-accessor"
    display_name = "GSM Accessor Service Account for Testing"
    project      = data.google_project.okd_homelab.project_id
}

resource "google_secret_manager_secret_iam_member" "test_secret_access" {
    project   = data.google_project.okd_homelab.project_id
    secret_id = "test-secret"
    role      = "roles/secretmanager.secretAccessor"
    member    = "serviceAccount:${google_service_account.test_gsm_accessor.email}"
}

resource "google_service_account_iam_member" "smoke_tests_wif_binding" {
    service_account_id = "projects/${data.google_project.okd_homelab.project_id}/serviceAccounts/${google_service_account.test_gsm_accessor.email}"
    role               = "roles/iam.workloadIdentityUser"
    member             = "principal://iam.googleapis.com/projects/${data.google_project.okd_homelab.number}/locations/global/workloadIdentityPools/${google_iam_workload_identity_pool.okd_pool.workload_identity_pool_id}/subject/system:serviceaccount:smoke-tests:smoke-tests-sa"
}

resource "google_service_account" "gsm_accessor" {
    account_id   = "gsm-accessor"
    display_name = "GSM Accessor Service Account"
    project      = data.google_project.okd_homelab.project_id
}

resource "google_secret_manager_secret_iam_member" "gsm_secret_access" {
    for_each  = toset(local.avp_gsm_secrets)

    project   = data.google_project.okd_homelab.project_id
    secret_id = each.value
    role      = "roles/secretmanager.secretAccessor"
    member    = "serviceAccount:${google_service_account.gsm_accessor.email}"
}

resource "google_service_account_iam_member" "argocd_wif_binding" {
    service_account_id = "projects/${data.google_project.okd_homelab.project_id}/serviceAccounts/${google_service_account.gsm_accessor.email}"
    role               = "roles/iam.workloadIdentityUser"
    member             = "principal://iam.googleapis.com/projects/${data.google_project.okd_homelab.number}/locations/global/workloadIdentityPools/${google_iam_workload_identity_pool.okd_pool.workload_identity_pool_id}/subject/system:serviceaccount:argocd:argocd-argocd-repo-server"
}

resource "google_service_account" "cert_manager_dns_solver" {
    account_id   = "cert-manager-dns-solver"
    display_name = "Cert Manager dns01 Solver Service Account"
    project      = data.google_project.okd_homelab.project_id
}

resource "google_project_iam_custom_role" "cert_manager_dns_solver_role" {
    role_id     = "cert_manager_dns_solver_role"
    title       = "Cert Manager DNS Role"
    description = "Least privilege role for cert-manager to manage Cloud DNS"
    project     = data.google_project.okd_homelab.project_id
    permissions = [
        "dns.resourceRecordSets.create",
        "dns.resourceRecordSets.delete",
        "dns.resourceRecordSets.get",
        "dns.resourceRecordSets.list",
        "dns.resourceRecordSets.update",
        "dns.changes.create",
        "dns.changes.get",
        "dns.changes.list",
        "dns.managedZones.list",
    ]
}

resource "google_project_iam_member" "cert_manager_dns_solver_role_binding" {
    project = data.google_project.okd_homelab.project_id
    role    = "projects/${data.google_project.okd_homelab.project_id}/roles/${google_project_iam_custom_role.cert_manager_dns_solver_role.role_id}"
    member  = "serviceAccount:${google_service_account.cert_manager_dns_solver.email}"
}

resource "google_service_account_iam_member" "cert_manager_wif_binding" {
    service_account_id = "projects/${data.google_project.okd_homelab.project_id}/serviceAccounts/${google_service_account.cert_manager_dns_solver.email}"
    role               = "roles/iam.workloadIdentityUser"
    member             = "principal://iam.googleapis.com/projects/${data.google_project.okd_homelab.number}/locations/global/workloadIdentityPools/${google_iam_workload_identity_pool.okd_pool.workload_identity_pool_id}/subject/system:serviceaccount:cert-manager:cert-manager"
}

# make k8s service account secretAccessor directly instead of via impersonation of google service account bc of eso limitations
resource "google_secret_manager_secret_iam_member" "openshift_monitoring_secret_accessor" {
    project   = data.google_project.okd_homelab.project_id
    secret_id = "alertmanager-discord-config"
    role      = "roles/secretmanager.secretAccessor"
    member    = "principal://iam.googleapis.com/projects/${data.google_project.okd_homelab.number}/locations/global/workloadIdentityPools/${google_iam_workload_identity_pool.okd_pool.workload_identity_pool_id}/subject/system:serviceaccount:openshift-monitoring:external-secrets"
}

# make k8s service account secretAccessor directly instead of via impersonation of google service account bc of eso limitations
resource "google_secret_manager_secret_iam_member" "quay_pull_secret_accessor" {
    project   = data.google_project.okd_homelab.project_id
    secret_id = "quay-jennweir-pull-secret"
    role      = "roles/secretmanager.secretAccessor"
    member    = "principal://iam.googleapis.com/projects/${data.google_project.okd_homelab.number}/locations/global/workloadIdentityPools/${google_iam_workload_identity_pool.okd_pool.workload_identity_pool_id}/subject/system:serviceaccount:argocd:external-secrets"
}

# Azure ------------------------------------------------------------------------------------------------------------------

resource "azuread_application" "okd_cluster" {
    display_name = "okd-cluster"
}

resource "azuread_service_principal" "okd_cluster" {
    client_id = azuread_application.okd_cluster.client_id
}
